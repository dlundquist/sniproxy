#!/usr/bin/perl

use strict;
use warnings;
use File::Basename;
use lib dirname (__FILE__);
use TestUtils;
use TestHTTPD;
use File::Temp;

sub proxy {
    my $config = shift;

    exec(@_, '../src/sniproxy', '-f', '-c', $config);
}

sub worker($$$$) {
    my ($hostname, $path, $port, $requests) = @_;

    for (my $i = 0; $i < $requests; $i++) {
        system('curl',
                '-s', '-S',
                '-H', "Host: $hostname",
                '-o', '/dev/null',
                "http://localhost:$port/$path");

        if ($? == -1) {
            die "failed to execute: $!\n";
        } elsif ($? & 127) {
            printf STDERR "child died with signal %d, %s coredump\n", ($? & 127), ($? & 128) ? 'with' : 'without';
            exit 255;
        } elsif ($? >> 8) {
            exit $? >> 8;
        }
    }
    # Success
    exit 0;
}

sub make_config($$$) {
    my $proxy_port1 = shift;
    my $proxy_port2 = shift;
    my $httpd_port = shift;

    my ($fh, $filename) = File::Temp::tempfile();
    my ($unused, $logfile) = File::Temp::tempfile();

    # Write out a test config file
    print $fh <<END;
# Minimal test configuration

listen 127.0.0.1 $proxy_port1 {
    proto http
    table table_a
    access_log $logfile
}

listen 127.0.0.1 $proxy_port2 {
    proto http
    table table_b
    access_log $logfile
}

table table_a {
    localhost 127.0.0.1 $httpd_port
}

table table_b  {
    localhost 127.0.0.1 $httpd_port
}
END

    close ($fh);

    return $filename;
}

sub alter_config($$$$) {
    my $filename = shift;
    my $proxy_port1 = shift;
    my $proxy_port2 = shift;
    my $httpd_port = shift;

    my $fh = undef;
    open($fh, '>', $filename)
        or die("open(): $!");

    # Write out a test config file
    print $fh <<END;
# Minimal test configuration

listen 127.0.0.1 $proxy_port1 {
    proto http
    table table_c
}

listen 127.0.0.1 $proxy_port2 {
    proto http
    table table_a
}

table table_a {
    localhost 127.0.0.1 $httpd_port
}

table table_c  {
    localhost 127.0.0.1 $httpd_port
}
END

    close ($fh);
}


sub main {
    my $proxy_port1 = $ENV{SNI_PROXY_PORT} || 8080;
    my $proxy_port2 = $ENV{SNI_PROXY_PORT2} || 8081;
    my $proxy_port3 = $ENV{SNI_PROXY_PORT3} || 8082;
    my $httpd_port1 = $ENV{TEST_HTTPD_PORT} || 8083;
    my $httpd_port2 = $ENV{TEST_HTTPD_PORT2} || 8084;
    my $workers = $ENV{WORKERS} || 10;
    my $iterations = $ENV{ITERATIONS} || 10;

    my $config = make_config($proxy_port1, $proxy_port2, $httpd_port1);
    my $proxy_pid = start_child('server', \&proxy, $config, @ARGV);
    my $httpd_pid = start_child('server', \&TestHTTPD::httpd, port => $httpd_port1);

    # Wait for proxy to load and parse config
    wait_for_port(port => $httpd_port1);
    wait_for_port(port => $proxy_port1);
    wait_for_port(port => $proxy_port2);

    for (my $i = 0; $i < $workers; $i++) {
        start_child('worker', \&worker, 'localhost', '', $proxy_port1, $iterations);
    }
    for (my $i = 0; $i < $workers; $i++) {
        start_child('worker', \&worker, 'localhost', '', $proxy_port2, $iterations);
    }

    # Wait for all our children to finish
    wait_for_type('worker');

    kill 15, $httpd_pid;

    # edit config
    alter_config($config, $proxy_port2, $proxy_port3, $httpd_port2);

    kill 1, $proxy_pid;

    $httpd_pid = start_child('server', \&TestHTTPD::httpd, port => $httpd_port2);
    wait_for_port(port => $httpd_port2);

    for (my $i = 0; $i < $workers; $i++) {
        start_child('worker', \&worker, 'localhost', '', $proxy_port2, $iterations);
    }
    for (my $i = 0; $i < $workers; $i++) {
        start_child('worker', \&worker, 'localhost', '', $proxy_port3, $iterations);
    }

    # Wait for all our children to finish
    wait_for_type('worker');


    # Give the proxy a second to flush buffers and close server connections
    sleep 1;

    # For troubleshooting connections stuck in CLOSE_WAIT state
    #kill 10, $proxy_pid;
    #system("netstat -ptn | grep $proxy_pid\/sniproxy");

    # For troubleshooting 100% CPU usage
    #system("top -n 1 -p $proxy_pid -b");

    # Orderly shutdown of the server
    kill 15, $proxy_pid;
    kill 15, $httpd_pid;
    sleep 1;

    # Delete our test configuration
    unlink($config);

    # Kill off any remaining children
    reap_children();
}

main();
